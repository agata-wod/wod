use os;
use uo;
use npc;
use util;
use cfgfile;

include "../pkg/npcs/main_ai/setup/setup";
include "../pkg/npcs/main_ai/main/mainloop_killpcs_dumb";
include "../pkg/npcs/main_ai/main/sleepmode_normal";
include "../pkg/npcs/main_ai/combat/fightmode_normal_default";
include "../pkg/npcs/main_ai/combat/closedistance_default";
include "../pkg/npcs/main_ai/combat/changeopponent_default";
include "../pkg/npcs/main_ai/combat/gethelp_default";

const HALT_THRESHOLD := 8; // how close before he attacks?
drop_anchor();

program KillPlayers()

	if (!me.backpack)
		DoOnCreationSetup ();
	endif
	DoStartupSetup ();

	if (getobjproperty (me, "frozen"))
		me.frozen := 1;
	endif

	SetWarMode (0);
	post_combat ();

	main_AI_loop ();

endprogram


function process_combat_event(ev)
	if (ev.type == EVID_DAMAGED)
		if (randomint(10) == 1)
			if (!GetObjProperty (me, "guardkill") and !GetObjProperty (me, "killme") )
				var it := createnpcfromtemplate (me.npctemplate, me.x , me.y-1, me.z, 0, me.realm);
				if (it)
					PrintTextAbove (me, me.name + " splits when struck!");
					SetObjProperty (it, "killme", 1);
				endif
			endif
		endif
		if (GetVital (me, "Life") < CINT (GetVitalMaximumValue (me, "Life")/10))
			EraseObjProperty (me, "#flees");
			Flee (ev.source);
		endif
	endif
endfunction


function in_combat_event_loop(opponent)
	DoSlimeDamage (opponent);
endfunction

function post_combat()

	DisableCombatEvents();
	EnableMainEvents();

	SetWarMode ( 0 );
	SetOpponent ( 0 );

	sleep(1);
	look_around();

endfunction

function prepare_for_fight(opponent)
	if (opponent)
	endif

	DisableMainEvents();
	EnableCombatEvents();

endfunction



function DoSlimeDamage (opponent)
	if (Distance (me, opponent) > 2)
		return;
	endif
	
	if (RandomInt (40))
		return;
	endif

	PrintTextAbovePrivate (me , me.name + " spits acid at you!", opponent);
	if (HasShield (opponent))
		return;
	endif

	var dmg := RandomInt(5) + 1;
	var things := ListEquippedItems( opponent );
	var item := things [CINT(RandomInt(len(things))+1)];

	//don't eat hair
	if (item.objtype >= 0x2030 and item.objtype <= 0x2060)
		return 0;
	endif
	
	//don't eat backpacks or mounts
	if (item.objtype == 0x0E75 or item.objtype == 0x1F021)
		return 0;
	endif

	//don't eat items without HP
	if (!item.maxhp)
		return 0;
	endif
	
	PrintTextAbovePrivate (item, "Acid damages "+ item.desc, opponent);
	if (dmg > item.hp)
		dmg := item.hp;
	endif
	item.hp := item.hp - dmg;
	if (item.hp <= 0)
		PrintTextAbovePrivate (opponent, item.desc + " is destroyed!", opponent);
		destroyitem (item);
	endif
endfunction


function HasShield (opponent)

	var shield := GetEquipmentByLayer (opponent, LAYER_HAND2);
	if (shield.objtype == UOBJ_ORDER_SHIELD or shield.objtype == UOBJ_CHAOS_SHIELD)
		PrintTextAbovePrivate (shield, "Your shield blocks the acid!", opponent);
		return 1;
	endif

	return 0;

endfunction
