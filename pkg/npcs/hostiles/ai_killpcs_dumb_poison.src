use os;
use uo;
use npc;
use util;
use cfgfile;

include "../pkg/npcs/main_ai/setup/setup";
include "../pkg/npcs/main_ai/main/mainloop_killpcs_dumb";
include "../pkg/npcs/main_ai/main/sleepmode_normal";
include "../pkg/npcs/main_ai/combat/fightmode_dumb";
include "../pkg/npcs/main_ai/combat/closedistance_default";
include "../pkg/npcs/main_ai/combat/changeopponent_dumb";
include "../pkg/npcs/main_ai/combat/gethelp_default";

const HALT_THRESHOLD := 8; // how close before he attacks?
drop_anchor();

program KillPlayers()
	if (!me.backpack)
		DoOnCreationSetup ();
	endif
	DoStartupSetup ();

	if (getobjproperty (me, "frozen"))
		me.frozen := 1;
	endif

	post_combat ();
	main_AI_loop ();
endprogram


function process_combat_event (byref ev)
	DoPoison (ev.source);
	if (ev.type == EVID_DAMAGED)
		if (GetVital (me, "Life") < CINT (GetVitalMaximumValue (me, "Life")/10))
			EraseObjProperty (me, "#flees");
			Flee (ev.source);
		endif
	endif
endfunction


function in_combat_event_loop (byref opponent)
	DoPoison (opponent);
endfunction

function post_combat()
	ClearEventQueue ();
	DisableCombatEvents();
	EnableMainEvents();

	SetWarMode ( 0 );
	sleep(1);
	look_around();
endfunction

function prepare_for_fight (byref opponent)
	if (opponent)
	endif
	ClearEventQueue ();
	DisableMainEvents();
	EnableCombatEvents();
endfunction

function DoPoison (byref opponent)
	if (Distance (me, opponent) > 1)
		return;
	endif

	if (RandomInt (4))
		return;
	endif

	if (!GetObjProperty (me, "#lastspell"))
		SetObjProperty (me, "#lastspell",ReadGameClock() + 2);
	endif

	if (GetObjProperty (me, "#lastspell") > ReadGameClock() )
		return;
	endif
	SetObjProperty (me, "#lastspell", ReadGameClock() + 6);

	var plvl := RandomInt (CINT (GetVitalMaximumValue (me, "Life")/2500))+1;
	if (plvl > 5)
		plvl := 5;
	endif
	SetPoison (opponent, plvl, "You have been poisoned by " + me.name);
endfunction
