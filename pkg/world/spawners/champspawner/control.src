use uo;
use os;
use util;
use cfgfile;

const INAC_WAIT := 300;		//  5 mins to sleep when inactive
const BIG_CHECK_WAIT:=5;	//  5 secs, checks for % of dead monsters  - ideal would be 1 here, but its a bit... heavy loop, so im using 5
const TINY_WAIT := 10;		// 10 msec to sleep between each spawn
const ACTIVE_EACH := 82800;	// 24 hours, activate each
const LVLDOWNCHECK := 600;	// checks for level down each 10 mins

include "champaltar";

program control(altar)
	var creatures := array;
	var kills:=0, totalKills:=0, nextLvlUp := 20;
	deactivateAltar(altar);		//erases any junk prop that might've passed by in the itemdesc
	activateAltar(altar);		//and starts it
	var nextLVLDownCheck:=ReadGameClock()+LVLDOWNCHECK;
	
	if (GetObjProperty (altar, "creatures"))
		kills := GetObjProperty (altar, "creatures");
		if (kills.size() > 0)
			creatures := kills;
		endif
		kills := 0;
	endif
	while(altar)
		if(CInt(GetObjProperty(altar, "active")))

//			PrintTextAbove(altar, "Congratz, you killed "+kills+" this time");
//			PrintTextAbove(altar, "Total Kills: "+totalKills);
//			PrintTextAbove(altar, "Thats "+killPercent+"%");
			
			kills:=0;
			var champserial:=GetObjProperty(altar, "champ");

			if(!champserial)		// Ok, champ didn't spawn yet, lets see how many creatures the player killed and respawn
				if(GetObjProperty(altar, "#completed"))
					spawnChampion(altar);
					champserial:=GetObjProperty(altar, "champ");
				else
					var i;

					for(i:=creatures.size(); i>0;i-=1)
						if(!SystemFindObjectBySerial(creatures[i]))
							creatures.erase(i);
							kills+=1;
						endif
					endfor
					SetObjProperty (altar, "creatures", creatures);
					totalKills += kills;
					var killPercent := (totalKills*100) / CInt(get_maxMonsters(altar));
					if(killPercent>90)
						if(!GetObjProperty(altar, "#completed"))
							kills:=0;
							totalKills:=0;
							createNextRedSkull(altar);
							nextLvlUp := 20;
							nextLVLDownCheck:=ReadGameClock()+LVLDOWNCHECK;
						else
							spawnChampion(altar);
						endif
					elseif (killPercent>nextLvlUp)
						createNextWhiteSkull(altar);
						nextLvlUp += 20;
						SetObjProperty (altar, "creatures", creatures);
					elseif(ReadGameClock()>nextLVLDownCheck)
						kills:=0;
						totalKills:=0;
						if (killPercent>20)
							createNextRedSkull(altar);
						endif
						nextLvlUp := 20;
						destroyLastRedSkull(altar);
						nextLVLDownCheck:=ReadGameClock()+LVLDOWNCHECK;
						SetObjProperty (altar, "creatures", creatures);
					endif
					if (!GetObjProperty(altar, "#completed"))
						while(creatures.size()<=(50-get_subLevel(altar)) && altar)
//						while(creatures.size()<=(15-get_subLevel(altar)) && altar)
							var loc:= getSpawnLocation(altar);
							var creature:=CreateNPCFromTemplate(get_spawn(altar), loc[1], loc[2], loc[3], 0, altar.realm);
							if(creature)
								creatures.append(creature.serial);
							endif
							sleepms(TINY_WAIT);
						endwhile
						SetObjProperty (altar, "creatures", creatures);
					endif
				endif
			elseif(!SystemFindObjectBySerial(champserial))	// Champ is dead
				deactivateAltar(altar);
			endif
				
			sleep(BIG_CHECK_WAIT);
		else
			if(ReadGameClock()>CInt(GetObjProperty(Altar, "NextActiv")))
				activateAltar(altar);
			else
				sleep(INAC_WAIT);
			endif
		endif

	endwhile

	return 0;
endprogram