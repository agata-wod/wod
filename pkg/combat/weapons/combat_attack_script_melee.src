use uo;
use os;
use util;
use cfgfile;

include "../pkg/combat/weapons/combat_attack_script";
include "include/newperformaction";

var attacker, defender;
var weaponcfgfile := ReadConfigFile (":weapons:itemdesc");
var armorcfgfile := ReadConfigFile (":armor:itemdesc");
var attacker_npcdesc_elem, defender_npcdesc_elem;
var attacker_weapon_elem;
var attacker_weapon, defender_armor, defender_weapon;

program combat_attack_script (parms)
	attacker := parms [1];
	defender := parms [2];
	if (!attacker or !defender)
		SendSysMessage (attacker, "No defender!");
		SendSysMessage (defender, "No attacker!");
		return 0;
	endif

	if (attacker.z > (defender.z + 20) or attacker.z < (defender.z - 20))
		return 1;
	endif
	if (Distance (attacker, defender) > 20)
		return 1;
	endif

	//Make sure that these files exist
	if (!weaponcfgfile or !armorcfgfile)
		SendSysMessage (attacker, "Unable to load itemdesc.cfg file(s)!");
		SendSysMessage (defender, "Unable to load itemdesc.cfg file(s)!");
		SendSysMessage (attacker, "Please notify a GM so that this problem can be fixed.");
		SendSysMessage (defender, "Please notify a GM so that this problem can be fixed.");
		return 0;
	endif

	//Load the NPCdesc elems for any parties to this combat that are NPCs
	if (attacker.npctemplate)
		attacker_npcdesc_elem := GetNpcTemplateElem (attacker.npctemplate);
	endif
	if (defender.npctemplate)
		defender_npcdesc_elem := GetNpcTemplateElem (defender.npctemplate);
	endif

	//Load the itemdesc entries for the attacker's weapon, if they have one
	if (attacker.weapon)
		attacker_weapon := attacker.weapon;
		attacker_weapon_elem := FindConfigElem (weaponcfgfile, attacker_weapon.objtype);
		if (attacker_weapon.hp or attacker_weapon.hp == 0)
			//Do some damage to the weapon
			if (RandomInt (100) == 1)
				if (DoDamageToEquipment(attacker_weapon, 1))
					if (attacker_weapon.desc)
						SendSysMessage (attacker, "Your " + attacker_weapon.desc + " breaks!");
						if (attacker.npctemplate and GetObjProperty (attacker, "master"))
							PrintTextAbove (attacker, "My weapon broke!");
						endif
					endif
					DestroyItem (attacker_weapon);
				endif
			endif
		endif
	endif
	
	if (defender.weapon)
		defender_weapon := defender.weapon;
	endif

	//Properly credit the attacker for anything that happens, but not in the arena
	if (attacker.x >= 1385 and attacker.x <= 1414 and attacker.y >= 3729 and attacker.y <= 3758)
		SetScriptController (0);
	else
		SetScriptController (attacker);
	endif
	

	//perform the attack animation.
	PerformAttackAnimation ();

	//Calc chance to hit and check for success
	var chance_to_hit := CalculateToHitChance ();
	if (RandomInt (100) > chance_to_hit)
		PlayMissSoundEffect ();
		return 1;
	endif
	//Hit! Figure out how much raw damage the attack did, then modify it be tactics and strength
	var raw_damage := CalculateAttackDamage ();
	var specialparms := WeaponSpecial (GetObjProperty (attacker, "#weaponability"), attacker, defender, raw_damage);
	// specialparms[1] = bypasses parry?
	// specialparms[2] = bypasses reactive armor?
	// specialparms[3] = bypasses armor?
	// specialparms[4] = bypasses critical hit?
	// specialparms[5] = overrides damage dealing? If -1, no damage, if 0, normal damage, if higher than 1, overrides damage
	if (specialparms[5] > 0)
		raw_damage := specialparms[5];
	endif
	var modified_damage := CINT ((raw_damage * (GetAttribute (attacker, ATTRIBUTEID_TACTICS) + 50 + CINT (GetAttribute (attacker, ATTRIBUTEID_STRENGTH)/5)))/100);
	if (attacker.race == RACE_GARGOYLE)
		var berserk_mod := 0.15 * CINT(5.0 - ((GetVital (attacker, "Hits") * 5.0) / GetVitalMaximumValue (attacker, "Hits")));
		modified_damage := CINT(modified_damage * (1.0 + berserk_mod));
	endif
		

	//Now check parry, if the defender has a shield
	var shield := GetEquipmentByLayer (defender, LAYER_HAND2);
	if (!specialparms[1] and shield and IsShield (shield))
		var defender_parry_skill := GetAttribute (defender, ATTRIBUTEID_PARRY);
		if (RandomInt (300) < defender_parry_skill)
			//Successful parry, reduce damage
			modified_damage := modified_damage - shield.ar;
			SendSysMessage (defender, "You parry the attack!");
			QuickAwardRawAttributePoints (defender, ATTRIBUTEID_PARRY, 30);
			NewPerformAction(defender, 1+RandomInt(2), 0, 0);
			PlaySoundEffect (defender, 0x143);

			//Do some damage to the shield
			if (shield.hp or shield.hp == 0)
				if (RandomInt (100) == 1)
					if (DoDamageToEquipment(shield, 1))
						SendSysMessage (defender, "Your shield breaks!");
						DestroyItem (shield);
					endif
				endif
			endif

			//if no damage left, quit now
			if (modified_damage <= 0)
				return 1;
			endif
		else	//On unsuccesfull parry, play hit sound effect
			PlayHitSoundEffect ();
		endif
	else	//On unsuccesfull parry, play hit sound effect
		PlayHitSoundEffect ();
	endif

	//Players take 1/2 damage
//	if (defender.acctname or defender.npctemplate == ":guards:warrior")
//		modified_damage := CINT (modified_damage/2);
//	endif
	
	//Combat advancement for weapon skills
	var weaponattributeid := attacker_weapon.attribute;
	if (!attacker_weapon or !weaponattributeid)
		weaponattributeid := ATTRIBUTEID_WRESTLING;
	endif

	QuickAwardRawAttributePoints (attacker, weaponattributeid, 30);
	QuickAwardRawAttributePoints (attacker, ATTRIBUTEID_TACTICS, 15);
	QuickAwardRawAttributePoints (defender, ATTRIBUTEID_TACTICS, 15);

	//fake hitscript stuff
	if (!specialparms[2] and GetObjProperty (defender, "#reactive_armor"))
		if (Distance (attacker, defender) <= 1)
			var evalintskill := GetAttribute (defender, ATTRIBUTEID_INVOCATION);
			var reactive_damage := CINT ( (evalintskill*modified_damage)/200);
			PlaySoundEffect (attacker, SFX_SPELL_EXPLOSION);
			PlayObjectCenteredEffect (attacker, FX_EXPLODE_3, 7, 0x10);
			DoDamageByType (defender, attacker, reactive_damage, DAMAGETYPE_MAGIC);
			EraseObjProperty (defender, "#reactive_armor");
		endif
	elseif (!specialparms[2] and GetObjProperty (defender, "special_reactive_armor") and RandomInt (2))
		if (Distance (attacker, defender) <= 1)
			var reactive_damage := CINT (modified_damage/2);
			PlaySoundEffect (attacker, SFX_SPELL_EXPLOSION);
			PlayObjectCenteredEffect (attacker, FX_EXPLODE_3, 7, 0x10);
			DoDamageByType (defender, attacker, reactive_damage, DAMAGETYPE_MAGIC);
		endif
	endif

	//Get the defender's AR rating and reduce the damage
	var defender_ar := CINT (GetDefendersAR (defender));
	if (!specialparms[3] and defender_ar > 0)
		modified_damage := CINT (modified_damage*(100 - defender_ar)/100);
	endif

	//Always do at least 1 damage
	if (modified_damage < 1)
		modified_damage := 1;
	endif
	//Do the damage
	if (specialparms[5] >= 0)
		DoDamageByType (attacker, defender, modified_damage, DAMAGETYPE_PHYSICAL);
	else
		return 1;
	endif

	//Check for critical hit
	if (!specialparms[4] and GetAttribute (attacker, ATTRIBUTEID_ANATOMY))
		var critical_multiplier := 1 + attacker_weapon_elem.HeavyCritical;
		if (!critical_multiplier)
			critical_multiplier := 1;
		endif
		var critical_chance := GetAttribute (attacker, ATTRIBUTEID_ANATOMY);
		if (RandomInt (1000) < critical_chance)
			SendSysMessage (attacker, "Critical hit!");
			if (CheckSkill (attacker, SKILLID_ANATOMY, 80, 0) )
				DoDamageByType (attacker, defender, CINT (modified_damage * critical_multiplier), DAMAGETYPE_PHYSICAL);
			else
				DoDamageByType (attacker, defender, CINT ((modified_damage * critical_multiplier)/2), DAMAGETYPE_PHYSICAL);
			endif
		endif
	endif

	if (GetObjProperty (attacker_weapon, "poison_level"))
		DoWeaponPoisonHit (attacker, defender, attacker_weapon);
	endif

	if (GetObjProperty (attacker_weapon, "silver") ) 
		DoSilverHit (attacker, defender, modified_damage);
	endif

	if (GetObjProperty (attacker_weapon, "specialweaponeffect") ) 
		DoEtherialEffect (attacker, defender, attacker_weapon, modified_damage);
	elseif (GetObjProperty (attacker_weapon, "#specialweaponeffect") ) 
		DoTemporaryWeaponBonusEffect (attacker, defender, attacker_weapon, modified_damage);
	endif
	
	DoHitSpell (attacker, defender, attacker_weapon);

	//Questie thing
	if (defender.npctemplate["void"])
		if (attacker_weapon.attribute == ATTRIBUTEID_FENCING or attacker_weapon.attribute == ATTRIBUTEID_SWORDSMANSHIP)
			SendSysmessage (attacker, "The void eats away at your blade!");
		else
			SendSysmessage (attacker, "The void eats away at your weapon!");
		endif
		if (DoDamageToEquipment (attacker_weapon, 5 + RandomInt (5)))
			DestroyItem (attacker_weapon);
			SendSysMessage (attacker, "The void has consumed your weapon!");
		endif
	endif

	return 1;
endprogram




///////////////////
//  Perform the attack animation for both players and NPCs.  If the graphic of the attacker isn't
//  a normal human, different animations need to be used
///////////////////

function PerformAttackAnimation ()
	//If they're not human, use different attack animations based on their graphic
	if (attacker.graphic != 400 and attacker.graphic != 401 and attacker.graphic != 605 and attacker.graphic != 606 and attacker.graphic != 666 and attacker.graphic != 667 and attacker.graphic != 744 and attacker.graphic != 745)
		case (attacker.graphic)
			CID_ALLIGATOR:
			CID_PIG:
			CID_BROWN_BEAR:
			CID_GRIZZLY_BEAR:
			CID_POLAR_BEAR:
			CID_BOAR:
			CID_BUNNY:
			CID_CAT:
			CID_DOG:
			CID_WOLF:
			CID_COW:
			CID_COW2:
			CID_BULL:
			CID_BULL2:
			CID_CHICKEN:
			CID_GOAT:
			CID_RAT:
			CID_GIANT_RAT:
			CID_PANTHER:
				PerformAction (attacker, (RandomInt (3) + 5));
			CID_EAGLE:
				PerformAction (attacker, 4);
			default:
				PerformAction (attacker, (RandomInt (3) + 4));
		endcase
	
		return;
	endif
	var anim;
	//If the player is mounted, use special mount animations, if they're defined in
	//the weapon's itemdesc.cfg entry, or a default animation if not
	if (GetEquipmentByLayer (attacker, LAYER_MOUNT) and (attacker.graphic != 666 and attacker.graphic != 667))
		anim := CINT (attacker_weapon_elem.MountedAnim);
		if(anim == 0x1D)
			anim := 0;
		endif
	else
		anim := attacker_weapon_elem.Anim;
	endif
	var newanim := 0;
	case (anim)
		0x09:  newanim := 4;
		0x0A: newanim := 5;
		0x0B: newanim := 3;
		0x0C: newanim := 6;
		0x0D: newanim := 7;
		0x0E: newanim := 8;
		0x12: newanim := 1;
		0x13: newanim := 2;
		0x1A: newanim := 7;
		default: newanim := 0;
	endcase
	NewPerformAction(attacker, 0, newanim, RandomInt(4));
	return;
endfunction




///////////////////
//  Calculate the odds of a successful attack
//////////////////

function CalculateToHitChance ()
	//First calculate the weaponskills for the attacker and defender
	var attacker_weaponskill := 0;
	if (attacker.acctname)
		if (attacker_weapon.attribute)
			attacker_weaponskill := GetAttribute (attacker, (attacker_weapon.attribute));
		else
			attacker_weaponskill := GetAttribute (attacker, ATTRIBUTEID_WRESTLING);
		endif

		//Increased chance to hit for players
		if (attacker_weaponskill > 95)
			attacker_weaponskill := CINT (attacker_weaponskill * 1.4);
		elseif (attacker_weaponskill > 75)
			attacker_weaponskill := CINT (attacker_weaponskill * 1.25);
		endif
	else
		if (attacker_weapon.attribute)
			attacker_weaponskill := GetAttribute (attacker, (attacker_weapon.attribute));
		else
			attacker_weaponskill := GetAttribute (attacker, attacker_npcdesc_elem.AttackAttribute);
		endif
		if (!attacker_weaponskill)
			attacker_weaponskill := GetAttribute (attacker, ATTRIBUTEID_WRESTLING);
		endif
	endif

	var defender_weaponskill := 0;
	if (defender.acctname)
		if (defender_weapon)
			defender_weaponskill := GetAttribute (defender, (defender_weapon.attribute));
		else
			defender_weaponskill := GetAttribute (defender, ATTRIBUTEID_WRESTLING);
		endif

		//Increased chance of being missed for players
		if (defender_weaponskill > 70)
			defender_weaponskill := CINT (defender_weaponskill * 1.20);
		endif
	else
		if (defender_weapon)
			defender_weaponskill := GetAttribute (defender, (defender_weapon.attribute));
		else
			defender_weaponskill := GetAttribute (defender, defender_npcdesc_elem.AttackAttribute);
		endif
		if (!defender_weaponskill)
			defender_weaponskill := GetAttribute (defender, ATTRIBUTEID_WRESTLING);
		endif
	endif

	var chance_to_hit := CINT (((attacker_weaponskill + 50)*50)/(defender_weaponskill + 50));

	//There's always a small chance to hit
	if (!chance_to_hit)
		chance_to_hit := 5;
	endif

	//Player made exceptional items have a slightly higher chance to hit
	if (attacker_weapon)
		var l_name := Lower (attacker_weapon.desc);
		if (l_name ["xceptional"])
			chance_to_hit := chance_to_hit + 5;
		endif
	endif

	//Silver weapons give bonus to hit against undead
	if (GetObjProperty (attacker_weapon, "silver") ) 
		if (IsAnUndeadCreature (defender.npctemplate))
			chance_to_hit := chance_to_hit + 5;
		endif
	endif

	//Special bonus to hit from some spells.  Set up as an array - {amount, endtime, name (for .status)}
	if (GetObjProperty (attacker_weapon, "#combat_to_hit_bonus"))
		var bonus_to_hit := GetObjProperty (attacker_weapon, "#combat_to_hit_bonus");
		var endtime := bonus_to_hit[2];
		if (endtime and endtime < ReadGameClock())
			EraseObjProperty (attacker_weapon, "#combat_to_hit_bonus");
		else
			chance_to_hit := chance_to_hit + bonus_to_hit[1];
		endif
	endif

	return chance_to_hit;
endfunction




///////////////////
//  Figures out the raw weapon damage, unmodified by tactics, armor, etc
///////////////////

function CalculateAttackDamage ()
	var raw_damage := 0;
	if (len (attacker_weapon_elem.Damage))
		raw_damage := RandomDiceRoll ((attacker_weapon_elem.Damage));
	else
		SendSysMessage (attacker, "Unable to find itemdesc damage entry for your weapon!");
		raw_damage := RandomDiceRoll ("4d6+3");
	endif
	if (attacker_weapon.dmg_mod)
		raw_damage := raw_damage + attacker_weapon.dmg_mod;
	endif

	//Now modify for weapon quality and HP
	if (attacker_weapon.quality and attacker_weapon.hp)
		raw_damage := CINT (raw_damage * attacker_weapon.hp * attacker_weapon.quality / attacker_weapon.maxhp);
	endif

	return raw_damage;
endfunction




///////////////////
//  Miss!  Play a swoosh-type sound effect
///////////////////

function PlayMissSoundEffect ()
	var sound_effect := attacker_weapon_elem.MissSound;
	if (!sound_effect)
		sound_effect := 0x239;
	endif
	PlaySoundEffect (attacker, sound_effect);
	return 1;
endfunction




///////////////////
//  Hit!  Play the hit sound effect
///////////////////

function PlayHitSoundEffect ()
	var sound_effect := attacker_weapon_elem.HitSound;
	if (!sound_effect)
		sound_effect := 0x23c;
	endif
	PlaySoundEffect (defender, sound_effect);
	return 1;
endfunction




///////////////////
//  This function is called if the weapon is poisoned
///////////////////

function DoWeaponPoisonHit (byref attacker, byref defender, byref weapon);
	var poison_strength := CINT (GetObjProperty (weapon, "poison_level"));
	var effective_strength := poison_strength;
	var character_skill := GetAttribute (attacker, ATTRIBUTEID_POISONING);
	if (character_skill < 50 and effective_strength > 2)
		effective_strength := 2;
	elseif (character_skill < 70 and effective_strength > 4)
		effective_strength := 4;
	elseif (character_skill < 90 and effective_strength > 7)
		effective_strength := 7;
	endif

	if (!effective_strength)
		return;
	endif

	if (defender.poisoned and (RandomInt (10) < effective_strength))
		return;
	endif

	var skill := 90;
	case (effective_strength)
		1:
		2:	skill := 20;
		3:
		4:	skill := 40;
		5:
		6:	skill := 60;
		7:
		8:	skill := 80;
		9:
		10:	skill := 100;
	endcase

	if (CheckSkill (attacker, SKILLID_POISONING, skill, 0))
		SetPoison (defender, effective_strength, "You have been poisoned by " + attacker.name);
		AddVirtue (attacker, -1);
	endif

	if (RandomInt(10) == 1)
		poison_strength := poison_strength - 1;
		SendSysMessage (attacker, "The poison on your weapon seems to have rubbed off some.");
	else
		return;
	endif

	if (poison_strength and poison_strength > 0)
		SetObjProperty (weapon, "poison_level", poison_strength);
	else
		SendSysMessage (attacker, "The poison on your weapon seems to have worn off.");
		EraseObjProperty (weapon, "poison_level");
	endif
endfunction




///////////////////
//  Called when the weapon being used is silver
///////////////////

function DoSilverHit (byref attacker, byref defender, rawdamage);
	if (!IsAnUndeadCreature (defender.npctemplate))
		return;
	endif

	PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
	DoDamageByType (attacker, defender, CINT (rawdamage/2)+1, DAMAGETYPE_PHYSICAL);

	if (GetObjProperty (defender, "summoned"))
		PlaySoundEffect (attacker, SFX_SPELL_MANA_VAMPIRE);
		if (!checkskill (defender, SKILLID_MAGICRESISTANCE, -1, 0))
			PlayStationaryEffect (defender.x, defender.y, defender.z, FX_SMOKE, 0xA, 0xA, 0, defender.realm );
			KillNPC (defender);
		endif
	endif
endfunction




///////////////////
//  Special kewl effects for etherial items
///////////////////

function DoEtherialEffect (attacker, defender, weapon, rawdamage)
	case (GetObjProperty (weapon, "specialweaponeffect"))
		"lightning":
			if (RandomInt (10) == 1)
				var spellparms := array { };
				spellparms[1] := "#SPECIALNONE";
				spellparms[2] := defender;
				start_script (":damage_spells:lightning", spellparms);		
			endif
		
		"vampire":
			var bonushp := GetVital (attacker, "Life") + rawdamage*10;
			if (bonushp > GetVitalMaximumValue (attacker, "Life"))
				bonushp := GetVitalMaximumValue (attacker, "Life");
			endif
			SetVital (attacker, "Life", bonushp);
			PlayObjectCenteredEffect (defender, FX_SPARK_EFFECT, 7, 0x10);
		
		"dragonslaying":
			var package := FindNPCPackage (defender.npctemplate);
			var NpcElem := GetNpcTemplateElem (defender.npctemplate);
			if ((package and package == "dragons") or NpcElem.dragon)
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif

		"orcslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "orcs")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif

		"trollslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "trolls")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif

		"ratmanslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "ratmen")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif
			
		"ophidianslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "ophidians")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif
				
		"terathanslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "terathans")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif	

		"lizardmanslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "lizardmen")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif
			
		"vampireslaying":
			var package := FindNPCPackage (defender.npctemplate);
			if (package and package == "vampires")
				PlayObjectCenteredEffect (defender, FX_GLOW, 7, 7);
				DoDamageByType (attacker, defender, CINT (rawdamage/2), DAMAGETYPE_MAGIC);
			endif
	endcase
endfunction





function DoHitSpell (byref attacker, byref defender, byref weapon)
	var spellname := getobjproperty(weapon,"hitspell");
	if (!spellname)
		return;
	endif

	//tempcode to fix old hitspell names:
	if (!spellname[":"])
		spellname := GetNewSpellName (spellname);
		setobjproperty (weapon, "hitspell", spellname);
	endif

	if (spellname["feeblemind"])
		if (!CanMod(defender,"cint") )
			return;
		endif
	elseif (spellname["clumsy"])
		if (!CanMod(defender,"cdex") )
			return;
		endif
	elseif (spellname["weaken"])
		if (!CanMod(defender,"cstr") )
			return;
		endif
	elseif (spellname["curse"])
		if (!CanMod(defender,"call") )
			return;
		endif
	endif

	if (GetObjProperty (weapon, "charges") )
		var charges := GetObjProperty (weapon, "charges");
		charges := charges - 1;
		if (!charges or charges < 1)
			EraseObjProperty (weapon, "charges");
			EraseObjProperty (weapon, "hitspell");
			var spellname := GetObjProperty (weapon, "itemspellname");
			if (spellname)
				var itemname := weapon.desc;
				if (itemname[CSTR ("of " + spellname)])
					itemname[CSTR ("of " + spellname)] := "";
				else
					itemname[CSTR ("and " + spellname)] := "";
				endif
				weapon.name := itemname;
			endif
			if (GetObjProperty (weapon, "oldname") )
				var itemname := GetObjProperty (weapon, "oldname");
				if (itemname[CSTR ("of " + spellname)])
					itemname[CSTR ("of " + spellname)] := "";
				else
					itemname[CSTR ("and " + spellname)] := "";
				endif
				SetObjProperty (weapon, "oldname", itemname);
			endif
		else
			SetObjProperty (weapon, "charges", charges);
		endif
	endif

	var spellparms := array { };
	spellparms[1] := "#MOB";
	spellparms[2] := attacker;
	spellparms[3] := defender;
	start_script (spellname, spellparms);
endfunction

function GetNewSpellName( spellname )

	case (spellname)
	"heal":
	"nightsight":
	"gheal":
	"manadrain":
	"reflect":
	"invisibility":
	"reveal":
	"manavamp":
	"resurrect":
		return (":misc_spells:"+spellname);
	"magicarrow":
	"harm":
	"fireball":
	"lightning":
	"mindblast":
	"ebolt":
	"explosion":
	"chain_lightning":
	"fstrike":
	"meteor_swarm":
	"earthquake":
		return (":damage_spells:"+spellname);
	"polymorph":
	"masscurse":
	"paralyze":
	"clumsy":
	"feeblemind":
	"reactivearmor":
	"weaken":
	"agility":
	"cunning":
	"protection":
	"strength":
	"bless":
	"archprot":
	"curse":
		return (":statmod_spells:"+spellname);
	"cure":
	"poison":
	"archcure":
		return (":poison_spells:"+spellname);
	"teleport":
	"recall":
	"gate":
		return (":travel_spells:"+spellname);
	"wallofstone":
	"firefield":
	"dispel_field":
	"poisonfield":
	"parafield":
	"energy_field":
		return (":field_spells:"+spellname);
	"blade_spirit":
	"summon_creature":
	"dispel":
	"massdispel":
	"vortex":
	"summon_air":
	"summon_daemon":
	"summon_earth":
	"summon_fire":
	"summon_water":
		return (":summoning_spells:"+spellname);
	endcase

endfunction




///////////////////
//  Druid spell 'Gaea's Fury' adds lightning damage on strike, but this is written to be
//  general purpose.  Property is an array of the type {effectname, endtime, spell name (for .status)}
///////////////////

function DoTemporaryWeaponBonusEffect (attacker, defender, attacker_weapon, modified_damage)
	if (attacker and modified_damage)
	endif
	var tempbonus := GetObjProperty (attacker_weapon, "#specialweaponeffect");
	if (tempbonus [2])
		if (ReadGameClock() > tempbonus[2])
			EraseObjProperty (attacker_weapon, "#specialweaponeffect");
			return;
		endif
	endif
	case (tempbonus[1])
		"lightning":
			if (RandomInt (4) == 1)
				var spellparms := array { };
				spellparms[1] := "#SPECIALNONE";
				spellparms[2] := defender;
				start_script (":damage_spells:lightning", spellparms);		
			endif
	endcase
endfunction



