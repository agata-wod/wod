use uo;
use os;
use util;

include "include/magic";
var spell_circle := 4;

program mana_drain ( parms, spellid )
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms, spellid))
		return 0;
	endif
	var cast_on := GetSpellCastOn (caster, parms, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS);
	if (!cast_on)
		return 0;
	endif

	var tempvar := cast_on;
	cast_on := CheckMagicReflection (caster, cast_on);
	if (!cast_on)
		return 0;
	elseif (cast_on == caster)
		caster := tempvar;
	endif

	var diff := (GetAttribute (caster, "Intelligence") - GetAttribute (cast_on, "Intelligence"));
	if (diff > 40)
		diff := 40;
	elseif (diff < (0-40))
		diff := (0-40);
	endif

	if (cast_on.npctemplate)
		diff := diff + CINT (GetAttribute (caster, ATTRIBUTEID_MEDITATION)/3);
	endif

	diff := Resisted (caster, cast_on, diff);
	if( diff > CINT (GetVital (cast_on, "Mana")/100))
		diff := CINT (GetVital (cast_on, "Mana")/100);
	endif

	if ( diff <= 0 )
		if (caster != cast_on)
			PrintTextAbovePrivate( caster , cast_on.name + "'s will is too strong!" , caster );
		endif
		return 1;
	endif

	PlaySoundEffect (caster, SFX_SPELL_MANA_DRAIN);
	PlayObjectCenteredEffect (cast_on, FX_HEAL_EFFECT, 7, 0x10);
	PrintTextAbovePrivate( caster , "You drained " + diff + " mana" , caster);
	SetVital (cast_on, "Mana", GetVital (cast_on, "Mana") - diff*100);
	return 1;
endprogram
