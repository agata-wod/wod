use uo;
use os;
use util;

include "include/magic";
var spell_circle := 6;

program cast_mark (parms, spellid)
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms, spellid))
		return 0;
	endif

	if (caster.realm == REALM_BRITANNIA)
		if (caster.x > 5118)
			if (caster.x < 6143 and caster.y > 2303)
				// T2A
			elseif (caster.x > 6913 and caster.x < 7160 and caster.y > 255 and caster.y < 512)
				//Heartwood
			else
				//Dungeons
				sendsysmessage (caster, "Blocked!");
				return 0;
			endif
		elseif (caster.x > 2560 and caster.x < 2960 and caster.y > 1940 and caster.y < 2340)
			//seal off Buccaneer's Den from outside gates
			SendSysMessage (caster, "Your mark spell has somehow been interrupted.");
			return 0;
		endif
	elseif (caster.realm == REALM_BRITANNIA_ALT)
		sendsysmessage (caster, "Blocked!");
		return 0;	
	elseif (caster.realm == REALM_ILSHENAR)
		sendsysmessage (caster, "Blocked!");
		return 0;	
	elseif (caster.realm == REALM_MALAS)
		if (caster.x < 513)
			sendsysmessage (caster, "Blocked!");
			return 0;
		endif
	elseif (caster.realm == REALM_TERMUR)
		if (caster.y < 2840)
			sendsysmessage (caster, "Blocked!");
			return 0;
		endif
	endif

	SendSysmessage (caster, "Select a rune to mark.");
	var cast_on := Target (caster, TGTOPT_CHECK_LOS );
	if (!cast_on)
		return 0;
	endif

	if (!Accessible(caster, cast_on))
		SendSysmessage (caster, "You can't reach that!");
	endif

	if (cast_on.objtype != UOBJ_RECALL_RUNE)
		SendSysmessage(caster, "You can only cast that on a recall rune!");
		return 0;
	endif

	if (!ReserveItem(cast_on))
		SendSysMessage (caster, "You can't cast on that.");
		return 0;
	endif

	PlayObjectCenteredEffect (caster, FX_MARK_EFFECT, 10, 10);
	PlaySoundEffect (caster, SFX_SPELL_MARK);
	cast_on.SetDestination (caster.x, caster.y, caster.z, caster.realm);

	SetName (cast_on, "a marked recall rune");
	return 1;
endprogram
