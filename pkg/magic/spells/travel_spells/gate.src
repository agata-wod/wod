use uo;
use os;
use util;

include "include/magic";
include "../pkg/items/housing/house";
var spell_circle := 7;

program spell_gate (parms, spellid)
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms, spellid))
		return 0;
	endif

	SendSysmessage (caster, "Select a rune to gate from.");
	var cast_on := Target (caster, TGTOPT_CHECK_LOS );
	if (!cast_on)
		return 0;
	endif

	if (!Accessible (caster, cast_on))
		SendSysmessage (caster, "You can't reach that!");
		return 0;
	endif

	if (cast_on.objtype != UOBJ_RECALL_RUNE)
		SendSysmessage(caster, "You can only cast that on a recall rune!");
		return 0;
	endif

	var dest := cast_on.GetDestination();
	if (!dest)
		SendSysMessage (caster, "That rune has not been marked!");
		return 0;
	endif
	
	var sign := FindHouseSignAtLocation (dest.x, dest.y, dest.realm);
	if (sign)
		if (!IsAFriend (sign, caster))
			SendSysMessage (caster, "You can't gate to there!");
			return 0;
		endif
	endif

	DoGateSpellEffect (caster, dest.x, dest.y, dest.z, dest.realm);
	return 1;
endprogram

