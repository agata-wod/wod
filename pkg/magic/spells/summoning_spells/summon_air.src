use uo;
use os;
use util;

include "include/magic";
var spell_circle := 8;

program summon_air (parms, spellid)
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms, spellid))
		return 0;
	endif
	var cast_loc := GetSpellCastOn (caster, parms);
	if (!cast_loc)
		return 0;
	endif

	if (caster.acctname)
		//players can only have 1 pet of a type
		var summoned_serial := GetObjProperty (caster, "#summoned_elemental_air");
		if (summoned_serial)
			var summoned := SystemFindObjectBySerial (summoned_serial);
			if (summoned)
				SendSysMessage (caster, "You already have an air elemental!");
				return 0;
			endif
		endif

		var duration := 20 + caster_magery + (3 * caster_evalint);
		if (caster_evalint > 95)
			SummonCreature (caster, "summoned_airelemental_3", cast_loc, duration, 1, 0, 0, "#summoned_elemental_air");
		elseif (caster_evalint > 75)
			SummonCreature (caster, "summoned_airelemental_2", cast_loc, duration, 1, 0, 0, "#summoned_elemental_air");
		else
			SummonCreature (caster, "summoned_airelemental_1", cast_loc, duration, 1, 0, 0, "#summoned_elemental_air");
		endif
	else
		var duration := 20 + caster_magery + caster_evalint;
		SummonCreature (caster, "airelemental", cast_loc, duration);
	endif
	return 1;
endprogram
