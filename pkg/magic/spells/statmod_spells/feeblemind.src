use uo;
use os;
use util;

include "include/magic";
var spell_circle := 1;

program cast_feeblemind (parms, spellid)
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms, spellid))
		return 0;
	endif
	var cast_on := GetSpellCastOn (caster, parms, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS);
	if (!cast_on)
		return 0;
	endif

	cast_on := CheckMagicReflection (caster, cast_on);
	if (!cast_on)
		return 1;
	endif

	var mod_amount := CalcModAmount() * 2;
	mod_amount := Resisted (caster, cast_on, mod_amount);
	var duration := CalcCurseModDuration ();
	if (cast_on.npctemplate)
		var stunduration := CINT (caster_magery/5) - 10;
		if (stunduration > 3)
			SetObjProperty (cast_on, "#lastspell", ReadGameClock() + stunduration);
		endif
	endif

	PlaySoundEffect (cast_on, SFX_SPELL_CURSE);
	PlayObjectCenteredEffect (cast_on, FX_CURSE_EFFECT, 7, 7);
	DoMagicStatMod (cast_on, STATMOD_INT, 0-mod_amount, duration, "Feeblemind Spell");
	return 1;
endprogram
