use uo;
use os;
use cfgfile;

const SIGNSTART := 0xB95;
const SIGNEND := 0xC0E;

program itemwipe( you, text )

	var wipex := you.x;
	var wipey := you.y;
	var wiper := you.realm;
	var radius := 3;

	if (text)
		var params := SplitWords( text );
		wipex := CInt( params[1] );
		wipey := CInt( params[2] );
		if (CInt( params[3] ))
			radius := CInt( params[3] );
		endif
	endif

	var i := 0;
	var n := 0;
	SendSysMessage( you, "Wiping items near " + wipex +", "+ wipey +", "+ wiper);
	for (n := you.z-60; n < you.z + 60; n := n + 5 )
		foreach item in ListItemsNearLocation( wipex, wipey, n, radius, wiper )
			if ( (!IsDoor(item)) and (!IsSign(item)) )
				DestroyItem( item );
				i:=i+1;
			endif
		endforeach
	endfor
	SendSysMessage( you, "Total Items Nuked: " + i);

endprogram

function IsDoor(item)

	var itemdesc := ReadConfigFile( ":doors:itemdesc" );
//	var doortype;
	if (itemdesc[item.objtype].doortype)
		return 1;
	endif

	return 0;

endfunction

function IsSign( item )

	if ( item.objtype >= SIGNSTART and item.objtype <= SIGNEND )
		return 1;
	else
		return 0;
	endif

endfunction
