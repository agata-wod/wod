///////////////////
//  This program is called when you double click a beehive.  It produces honey and wax
///////////////////

use uo;
use os;
use util;
use attributes;

include "include/client";
include "include/objtype";
include "include/gumps";
include "include/crafting";

Const ITEM_SWARM		:= 0x091b;
Const SOUND_SCRUNCH		:= 0x58;
Const ACTION_BOW		:= 0x0020;

Const MAX_PER_HIVE	:= 10;

program harvest_honey (character, beehive)
	if (Distance (character, beehive) > 3)
		sendsysmessage (character, "That is too far away!");
		return;
	endif

	var already_harvested_amount := CInt(GetObjProperty (beehive, "#harvested"));

	//if there's no honey or wax, ask the user if they want to destroy the hive
	if (already_harvested_amount >= MAX_PER_HIVE)
		SendSysmessage (character, "The hive is empty.");
		SendSysmessage (character, "Do you want to destroy the beehive?");
		var destroy := YesNo (character, "Destroy?");
		if (destroy)
			CreateItemAtLocation (beehive.x, beehive.y, beehive.z, ITEM_SWARM, 1, beehive.realm);
			CreateItemAtLocation (beehive.x, beehive.y, beehive.z, ITEM_SWARM, 1, beehive.realm);
			DestroyItem (beehive);
			SendSysmessage (character, "You destroy the hive.  The bees are pretty angry...");
		endif
		return;
	endif

	SendSysmessage (character, "You begin harvesting.");
	var craftingbag := FindCraftingBag (character);
	var characterx := character.x;
	var charactery := character.y;
	repeat
		//action, sound, and time delay
		PlaySoundEffect( character, SOUND_SCRUNCH );
		PerformAction( character, ACTION_BOW );
		sleep (2);
		PlaySoundEffect( character, SOUND_SCRUNCH );
		PerformAction( character, ACTION_BOW );
		sleep (3);

		//do a skill check to see if they manage to harvest anything
		if (checkskill (character, SKILLID_LUMBERJACKING, -1, 0))
			already_harvested_amount := CInt(GetObjProperty (beehive, "#harvested"));
			if (!already_harvested_amount)
				SetObjProperty (beehive, "#harvested", 1);
			else
				SetObjProperty (beehive, "#harvested", CInt(already_harvested_amount + 1));
			endif
			
			CreateItemInContainer (craftingbag, UOBJ_HONEY, 1);
			CreateItemInContainer (craftingbag, UOBJ_WAX, 1);
		endif

		//See if they stir up any bees
		if (!checkskill (character, SKILLID_ANIMALLORE, -1, 0))
			if (RandomInt (4) == 1)
				CreateItemAtLocation (beehive.x, beehive.y, beehive.z, ITEM_SWARM, 1, beehive.realm);
				SendSysmessage (character, "Uh-oh...");
				return;
			endif
		endif
		
		if (already_harvested_amount >= MAX_PER_HIVE)
			SendSysmessage (character, "There's nothing left to harvest right now.");
			return;
		endif
	until (character.x != characterx or character.y != charactery);
	SendSysMessage (character, "You stop harvesting.");
endprogram
