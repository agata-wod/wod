use uo;
use os;
use util;
use cfgfile;

include "include/wodinc";
include "include/eventid";
include "include/utility";

Const MOVING_EFFECT_PURPLE_POTION  := 0x0F0D; 
Const SOUND_EFFECT_EXPLOSION	   := 0x208; 
Const EFFECT_EXPLOSION             := 0x36b0;

program use_explosion (parms)
	var mob := parms[1];
	var ttarget := parms[2];

	if (!mob or !ttarget)
		return;
	endif

	mob.hidden := 0;

	var x := ttarget.x;
	var y := ttarget.y;
	var z := ttarget.z;
	var rlm := ttarget.realm;

	if (!CheckLosAt (mob, x, y, z))
		return;
	endif

	PlayMovingEffectXYZ (mob.x, mob.y, mob.z+15,
    					ttarget.x, ttarget.y, ttarget.z,
                         MOVING_EFFECT_PURPLE_POTION, 8, 0, 0, mob.realm );
	Sleep(2);
	var lookout := CreateItemAtLocation (x, y, z, 0x0f0d, 1, rlm);
	ReserveItem (lookout);

	PrintTextAbove (lookout, "2");
	sleep (1);
	PrintTextAbove (lookout, "1");
	sleep (1);

	PlayStationaryEffect (x, y, z + 5, EFFECT_EXPLOSION, 7, 0x10, 0, rlm);
	DestroyItem (lookout);

	var ev := struct;
	ev.+type := EVID_DAMAGED;
	ev.+source := mob;

	foreach critter in ListMobilesNearLocationEx (x, y, z, 3, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN, rlm );
		if (!GetObjProperty (critter, "#specialhidden_nodamage"))
			PlaySoundEffectPrivate (critter, SOUND_EFFECT_EXPLOSION, critter);
			PlayObjectCenteredEffect (critter, EFFECT_EXPLOSION, 7, 0x10);
			if (!critter.npctemplate or GetObjProperty (critter, "master") or critter == ttarget)
				var dmg := RandomDiceRoll ("4d8+8");
				SendEvent (critter, ev); 
				DoDamageByType (mob, critter, dmg, DAMAGETYPE_FIRE);
			else
				var dmg := RandomDiceRoll ("2d10+10");
				DoDamageByType (mob, critter, dmg, DAMAGETYPE_FIRE);
			endif
		endif
	endforeach
endprogram
