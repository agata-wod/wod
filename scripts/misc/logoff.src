use os;
use uo;
use util;

include "include/client";
include "include/clock";
include "include/accounts";

program logoff (who)
	var humuc := GetObjProperty (who, "humuc");
	if (humuc)
		humuc := SystemFindObjectBySerial (humuc);
		if (humuc) 
			humuc.hidden := 1;
			humuc.frozen := 1;
		else
			EraseObjProperty (who, "humuc");
		endif
	endif

	set_critical (1);
		var logon_time := GetObjProperty (who, "logontime");
		if (!logon_time)
			set_critical (0);
			return;
		endif
		var play_time := ReadGameClock() - logon_time;

		//The semi-permanent counter
		var old_online_time := GetAccountProperty (who.acctname, "online_time");
		if (!old_online_time)
			old_online_time := 0;
		endif

		var total_online_time := old_online_time + play_time;
		SetAccountProperty (who.acctname, "online_time", total_online_time);
	
		//This months counter
		var this_month_string := GetCurrentPlaytimeMonth ();
		var old_monthly_play_time := GetAccountProperty (who.acctname, this_month_string);
		if (!old_monthly_play_time)
			old_monthly_play_time := 0;
		endif
	
		var total_monthly_play_time := old_monthly_play_time + play_time;
		SetAccountProperty (who.acctname, this_month_string, total_monthly_play_time);
		
		//add to the server number
		var total_monthly_string := "total_" + this_month_string;
		var total_monthly := GetGlobalProperty (total_monthly_string);
		if (!total_monthly)
			total_monthly := 0;
		endif
		total_monthly := total_monthly + play_time;
		SetGlobalProperty (total_monthly_string, total_monthly);
	set_critical (0);
endprogram
