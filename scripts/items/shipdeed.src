use os;
use uo;

include "include/client";
include "include/objtype";
include "include/utility";

program useshipdeed( character, deed )
    if (DeedAlreadyBuiltFrom( deed ))
        PrintTextAbovePrivate( deed, "That ship has already been built.", character );
    else
        BuildShip( character, deed );
    endif
endprogram



//
// BuildShip: build a ship from a deed.
// 
// The character is prompted to place the boat.  If the boat, the new keys, and
//    the ownership deed can be created, then they are, and the original deed is
//    destroyed.
//
// TODO: the location is tested to make sure it's on water, and not occupied,
//       but not much else beside that (ie closeness to the dock, or even the placer)
// 

function BuildShip( character, deed )
    var shiptype := GetObjProperty( deed, "shiptype" );

    if (!shiptype)
        PrintTextAbovePrivate( deed, "That ship deed appears to be broken.", character );
//        syslog( "Deed " + deed.serial + " had no shiptype property!" );
        return;
    endif

    shiptype := TurnedShiptype( shiptype, character.facing );

    var where := TargetMultiPlacement( character, shiptype );
    if (!where) // cancelled
        return;
    endif

        //
        // Do the work, atomically.
        //

    set_critical( 1 );

    var created := CreateShipKeysAndBuiltDeed( character, shiptype, where.x, where.y, where.z );
    if (!created) 
        return;     // note CreateShip.. prints appropriate error message
    endif

    DestroyItem( deed );

    created.ship.starboardplank.locked := 1;
    created.ship.portplank.locked := 1;
    created.ship.hold.locked := 1;

    var lockid := AllocLockId();
    SetObjProperty( created.ship.starboardplank,    "lockid", lockid );
    SetObjProperty( created.ship.portplank,         "lockid", lockid );
    SetObjProperty( created.ship.hold,              "lockid", lockid );
    SetObjProperty( created.packkey,                "lockid", lockid );
endfunction


//
// turned_shiptype 
//
// When a ship is placed, it should be shown facing perpendicular to the
// direction the placing character is facing.  This function calculates the
// correct multi-type to use given the base ship type and placer's facing.
//
// This assumes 'shiptype' is the base-shiptype (facing north) - 0x4000, 0x4004
//
// If the character is facing east, the ship's bow should face north.
// If the character is facing south, the ship's bow should face east.
// 

function TurnedShiptype( shiptype, facing )
    var boatadd := (facing / 2) - 1;
    if (boatadd < 0) 
        boatadd := boatadd + 4;
    endif

    shiptype := shiptype + boatadd;
    return shiptype;
endfunction


//
// CreateShipAndParaphernalia: atomically create ship, keys, and new deed
//
// The keys and deed, in the bankbox and backpack, are created first because
// it's possible to delete them.  It's nontrivial to delete the ship.
//
// Returns a structure with references to the created objects.
//

function CreateShipKeysAndBuiltDeed( character, shiptype, x, y, z )
    var packkey := CreateItemInBackpack( character, UOBJ_COPPER_KEY );
    if (!packkey)
        PrintTextAbovePrivate( character, "My backpack is too full!", character );
        return 0;
    endif

	var ship := CreateMultiAtLocation( x, y, z, shiptype, 0, character.realm );
	if (!ship)
		PrintTextAbovePrivate( character, "I can't place the ship there.", character );
		DestroyItem( packkey );
		return;
	endif

    var result := struct;
    
    result .+ packkey := packkey;
    result .+ ship := ship;
    
    return result;
endfunction


//
// A deed has been built from if it has a "builtserial" custom property.
//

function DeedAlreadyBuiltFrom( deed )
    if (GetObjProperty( deed, "builtserial" ))
        return 1;
    else
        return 0;
    endif
endfunction
