use os;
use uo;
use npc;
use util;
use cfgfile;

include "../pkg/npcs/main_ai/setup/setup";
include "../pkg/npcs/main_ai/main/mainloop_good";
include "../pkg/npcs/main_ai/main/sleepmode_normal";
include "../pkg/npcs/main_ai/combat/fightmode_normal_default";
include "../pkg/npcs/main_ai/combat/closedistance_spellcaster";
include "../pkg/npcs/main_ai/combat/gethelp_good";
include "../pkg/npcs/main_ai/combat/changeopponent_default";
include "../pkg/npcs/speech";

const HALT_THRESHOLD := 8; // how close before he attacks?
drop_anchor();

program KillPlayers ()
	if (!me.backpack)
		DoOnCreationSetup ();
	endif
	DoStartupSetup ();
	SetUpSpeech ();
	
	if (getobjproperty(me, "frozen"))
		me.frozen := 1;
	endif

	SetWarMode (0);
	post_combat ();
	main_AI_loop ();
endprogram

function process_combat_event (ev)

	SpellAttack (ev.source);

	case (ev.type)
		EVID_DAMAGED:
			if (GetVital (me, "Life") < CINT (GetVitalMaximumValue (me, "Life")/2))
				CastSpell (me, me,":misc_spells:gheal");
				if( me.poisoned )
					CastSpell (me, me, ":poison_spells:cure");
					CastSpell (me, ev.source, ":poison_spells:poison");
				endif
			endif
			if (GetVital (me, "Life") < CINT (GetVitalMaximumValue (me, "Life")/10))
				EraseObjProperty (me, "#flees");
				Flee (ev.source);
			endif
	endcase
endfunction

function spellattack (opponent)
	if (!GetObjProperty (me, "#lastspell"))
		SetObjProperty (me, "#lastspell",ReadGameClock() + 2);
	endif

	if (GetObjProperty (me, "#lastspell") > ReadGameClock() )
		return;
	endif

	if (GetVital (me, "Mana") < 1000)
		SetObjProperty (me, "#lastspell", ReadGameClock() + 10);
	endif

	if (GetVital (me, "Mana") < 6000 and GetVital (me, "Mana") >= 4000 and GetVital (opponent, "Mana") > 3000)
		if (GetAttribute (me, "Intelligence") > GetAttribute (opponent, "Intelligence") and RandomInt (2))
			TurnToward (opponent);
			sleep (1);
			CastSpell (me, opponent, ":misc_spells:manavamp");
			SetObjProperty (me, "#lastspell", ReadGameClock() + 2);
			return;
		endif
	endif

	if (RandomInt (3))
		sleep (1);
		SetObjProperty (me, "#lastspell", ReadGameClock() + 3);
		cast_offensive_spell (me, opponent);
		return;
	endif
endfunction

function in_combat_event_loop(opponent)

	spellattack(opponent);

endfunction

function prepare_for_fight(opponent)
	if(opponent)
	endif

	if (GetObjProperty (me, "mr"))
		CastSpell (me, me, ":misc_spells:reflect");
	endif

	DisableMainEvents ();
	EnableCombatEvents ();

endfunction

function post_combat()

	if ( me.hp < (me.strength/2))
		CastSpell(me,me,":misc_spells:gheal");
	endif

	DisableCombatEvents();
	EnableMainEvents();

	SetWarMode( 0 );
	SetOpponent( 0 );

	sleep(1);
	look_around();

endfunction
